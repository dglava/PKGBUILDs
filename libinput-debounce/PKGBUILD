# PKGBUILD modifed from https://aur.archlinux.org/packages/libinput-git/
pkgname=libinput-debounce
pkgver=1.9.3
pkgrel=1
pkgdesc='Input device management and event handling library - increased debounce timeout'
arch=(i686 x86_64)
url='http://freedesktop.org/wiki/Software/libinput/'
provides=("libinput=$pkgver")
license=('MIT')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
checkdepends=('valgrind')
makedepends=('git' 'meson' 'doxygen' 'graphviz' 'gtk3')
conflicts=('libinput')
source=("https://www.freedesktop.org/software/libinput/libinput-${pkgver}.tar.xz"
        "debounce-increase.patch")
sha1sums=('SKIP'
          'e25228e28c8df78a803a6f45d563b331e36a9a81')

prepare() {
  cd "$srcdir/libinput-${pkgver}"
  patch -p0 < "$srcdir/debounce-increase.patch"
}

build() {
	cd "libinput-${pkgver}"
	meson build --prefix=/usr \
	      --buildtype=release \
	      --libexecdir=/usr/lib \
	      -Dtests=false
	ninja -C build
}

package() {
	cd "libinput-${pkgver}"
	DESTDIR="$pkgdir" ninja -C build install

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
