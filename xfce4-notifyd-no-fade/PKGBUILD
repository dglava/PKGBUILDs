_pkgname=xfce4-notifyd
pkgname=xfce4-notifyd-no-fade
pkgver=0.3.0
pkgrel=3
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool')
provides=('notification-daemon')
conflicts=('xfce4-notifyd')
source=("http://archive.xfce.org/src/apps/$_pkgname/0.3/$_pkgname-$pkgver.tar.bz2" "no-fade.patch")
sha256sums=("8c7ed62f9496816d1391281f77d1b32216f9bf6fd22fbe4f6f3f4e07a6bbced0"
"980cee3fd94d28f3039a726768126dbb10e358cfab6435327474bb1c900d3601")

prepare() {
  cd "$srcdir/$_pkgname-$pkgver/"
  # removes the fade-out when the desktop is composited
  patch -p1 < "$srcdir/no-fade.patch"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
