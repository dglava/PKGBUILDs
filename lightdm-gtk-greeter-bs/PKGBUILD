# Copy pasted from the official Arch package
# Adds an additional desktop file which starts the greeter with the
# LANGUAGE variable set to bosnian
pkgname=lightdm-gtk-greeter-bs
pkgver=2.0.3
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM - updated Bosnian translation'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gobject-introspection' 'intltool')
conflicts=('lightdm-gtk-greeter')
replaces=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter' 'lightdm-gtk-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/lightdm-gtk-greeter/${pkgver%.?}/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz"
        "lightdm-gtk-greeter-bs.desktop")
sha1sums=('55f46ca92633e0613ced228b2c2bf06ba73cfc39'
          '56f4dc405d56a94d5d9657c06a3877fd10ab5ec1')
build() {
  cd lightdm-gtk-greeter-${pkgver}

  export CFLAGS="$CFLAGS -Wno-declaration-after-statement"
  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd lightdm-gtk-greeter-${pkgver}

  make DESTDIR="${pkgdir}" install
  # copy the desktop file for the greeter which selects the bosnian language.
  # this way the greeter's language is independent from the set system language.
  # to enable it change the greeter in lightdm.conf to lightdm-gtk-greeter-bs
  cp "$srcdir/lightdm-gtk-greeter-bs.desktop" "$pkgdir/usr/share/xgreeters/"
}
