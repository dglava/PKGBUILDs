_pkgname=mpdas
pkgname=mpdas-git
pkgver=0.4.3.r0.gd96b61e
pkgrel=1
pkgdesc='Music scrobbler for mpd'
arch=('x86_64' 'i686')
url='https://github.com/hrkfdn/mpdas.git'
license=('BSD')
depends=('libmpdclient' 'curl')
makedepends=('git')
source=("git+https://github.com/hrkfdn/mpdas.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  # change config file location to ~/.config/mpdas/
  sed -i 's/path.append("\/.mpdasrc");/path.append("\/.config\/mpdas\/mpdasrc");/g' main.cpp
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make PREFIX=/usr CONFIG=/etc
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make install PREFIX="$pkgdir"/usr MANPREFIX="$pkgdir/usr/share/man/man1"
  install -Dm644 "$srcdir/${pkgname%-git}"/mpdasrc.example "$pkgdir/etc/mpdasrc"
  install -Dm644 "$srcdir/${pkgname%-git}"/LICENSE "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE"
}
