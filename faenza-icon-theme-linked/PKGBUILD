pkgname=faenza-icon-theme-linked
pkgver=1.3.1
pkgrel=1
pkgdesc="Faenza icon theme - with symlink fixes"
arch=('any')
url="https://www.gnome-look.org/content/show.php/Faenza?content=128143"
license=('GPL')
options=(!strip)
depends=('adwaita-icon-theme')
source=("https://launchpad.net/~tiheum/+archive/ubuntu/equinox/+sourcefiles/faenza-icon-theme/${pkgver}/faenza-icon-theme_${pkgver}.tar.gz")
sha1sums=('9acfb7931482b4202d0e7ce158ccf030f7fc5d7b')

package() {
  mkdir -p ${pkgdir}/usr/share/icons/

  cd ${srcdir}/${pkgname%-linked}-${pkgver%.1}
  cp -r Faenza* ${pkgdir}/usr/share/icons/

  # fix https://gitlab.gnome.org/GNOME/glib/issues/1529
  cd ${pkgdir}/usr/share/icons/Faenza-Dark
  rm -rf places mimetypes
  ln -s /usr/share/icons/Faenza/places
  ln -s /usr/share/icons/Faenza/mimetypes

  # fix trashcan
  ln -s /usr/share/icons/Adwaita/scalable/status/user-trash-full-symbolic.svg \
    ${pkgdir}/usr/share/icons/Faenza/places/scalable/

  rm ${pkgdir}/usr/share/icons/Faenza/places/scalable/user-trash-symbolic.svg
  ln -s /usr/share/icons/Adwaita/scalable/places/user-trash-symbolic.svg \
    ${pkgdir}/usr/share/icons/Faenza/places/scalable/
}

