pkgname=qt5-qtstyleplugins
_pkgname=qtstyleplugins
pkgver=v5.0.0.r7.g5f2549b
pkgrel=1
pkgdesc="Additional style plugins for Qt5"
arch=('i686' 'x86_64')
url="http://code.qt.io/cgit/qt/qtstyleplugins.git"
license=('LGPL')
depends=('qt5-base' 'gconf' 'gtk2')
source=("git://code.qt.io/qt/qtstyleplugins.git")
sha1sums=(SKIP)

pkgver() {
  cd "$_pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$_pkgname"/src
    qmake PREFIX=/usr LIBDIR=/usr/lib
    make
}

package() {
    cd "$_pkgname"/src
    make INSTALL_ROOT="$pkgdir" install
    rm -rf "$pkgdir/usr/lib/cmake"
}
